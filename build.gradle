//this section is defined if gradlebuild depent on external plugins like boot-gradle-plugin
buildscript {
	ext {
		springBootVersion = '2.0.0.RELEASE'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath('io.spring.gradle:dependency-management-plugin:0.5.4.RELEASE')
	}
}

subprojects {
	apply plugin: 'java'
	apply plugin: 'idea'
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'

	group = 'uni.master'
	sourceCompatibility = 1.8

	repositories {
		mavenCentral()
		maven { url "https://repo.spring.io/milestone" }
	}

	ext {
		springCloudVersion = 'Finchley.M8'
	}

	dependencyManagement {
		imports {
			mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
		}
	}

}

task clean {
	dependsOn subprojects.clean
	doLast {
		println 'Build allsubprojects task :clean completed'
	}
}

task build {
	dependsOn subprojects.build
	doLast {
		println 'Build allsubprojects task :build completed'
	}
}

task dockerComposeUp(type:Exec, dependsOn: build) {
	workingDir './'
	ignoreExitValue true
	commandLine "docker-compose", "up", "--build"
}